#!/usr/local/bin/zsh

# crnt looks up the argument that was passed on crontab guru


setopt RE_MATCH_PCRE

if [[ $# != 1 ]]; then
  echo 'Needs one argument'
  return 1
fi

local allowed=(@monthly @daily @yearly @weekly @hourly)
# Check if the first argument is in the array
if (($allowed[(Ie)$1])); then
  open -u "https://crontab.guru/#$1"
  return 0
fi

# Get length of string minus 1
local str_len=$(( $#1 - 1 ))
local i
for i in {1..$str_len}; do
  # Character of string at the index
  local char=${1:$i:1}
  if [[ $char == ' ' ]]; then
    continue
  fi

  # Test if character is not one of these
  if [[ $char != '*' && $char != '/' && ! $char =~ '^\d+$' ]]; then
    echo "Invalid character: $char"
    return 1
  fi
done

# Check if the amount of words is not equal to five
if [[ ${#${=1}} != 5 ]]; then
  echo 'Needs five parts for valid cron time'
  return 1
fi

# Substitute spaces for underscores and forms url
local url="https://crontab.guru/#${1:gs/ /_}"
# Open url
open -u $url
