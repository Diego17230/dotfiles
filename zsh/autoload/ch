#!/bin/zsh

# ch changes to a directory nested anywhere within the current directory's
# subdirectories. Optional -p or --preview option for showing which directory
# it would change to


# ANSI escape sequence for bold text
readonly local BOLD=$(tput bold)
# ANSI escape sequence for plain text
readonly local PLAIN=$(tput sgr0)

# _enumerate() prints each of its arguments with a one-based number prefix
function _enumerate() {
  local i=1
  local path
  for path in $@; do
    # Removes the './' at the start of the element
    echo "$i. $BOLD${path#./}$PLAIN"
    (( i+=1 ))
  done
}

case $1 in
  '-p' | '--preview')
    local preview=true
    local search=$2
    if [[ -z $search ]]; then
      echo 'Need a search pattern'
      unfunction _enumerate
      return 1
    fi
    ;;
  '')
    echo 'Need at least one argument'
    unfunction _enumerate
    return 1
    ;;
  *)
    local search=$1
    if [[ $2 == '-p' || $2 == '--preview' ]]; then
      local preview=true
    fi
esac

local matches=($(fd "^$search.*" --type d))

case $#matches in
  1)
    local new_dir="$BOLD$matches[1]$PLAIN"
    if [[ ! -v preview ]]; then
      echo "Changed directory to $new_dir!"
      cd $matches[1]
    else
      echo "Would change directory to: $new_dir"
    fi
    ;;
  0)
    echo 'No matches found'
    unfunction _enumerate
    return 1
    ;;
  *)
    echo 'Multiple matches!'
    _enumerate $matches

    # Check if $preview is set
    if [[ -v preview ]]; then
      unfunction _enumerate
      return 0
    fi

    local result
    # Get user input for directory to change to
    vared -p 'Choose the directory: ' -c result
    # Check if user input is not a number or is not within bounds of $matches
    if [[ ! $result =~ '^[0-9]+$' || -z $matches[$result] ]]; then
      echo "Invalid input: $result"
      unfunction _enumerate
      return 1
    fi
    local new_dir="$BOLD$matches[$result]$PLAIN"
    echo "Changed directory to $new_dir!"
    cd $matches[$result]
esac

unfunction _enumerate
